// Section logger by Tale
// https://forum.cockos.com/showpost.php?p=2603763&postcount=1

desc:Section Logger

slider1:0.5<0.0,1.0,0.001>Slider 1
slider2:5<0,10,1>Slider 42
slider3:9<0,10,1{NULL,One,Two,Three,Five,Four,Six,Seven,Eight,Nice,Ten}>Slider 69

@init

ext_noinit = 1.0;

function log_section(id, multi)
  global(mutex, buf, len)
  local(ptr, end, n)
(
  while(atomic_setifequal(mutex, 0, 1));

  ptr = buf;
  end = buf + len;
  n = 0;

  multi ? (
    ptr = end - 1;
    len > 0 && (ptr[] & 7) == id ? (
      n = ptr[] >> 3;
    ) : (
      len += 1;
      ptr += 1;
    );
  ) : (
    while(ptr < end && (ptr[] & 7) != id) ( ptr += 1; );
    ptr < end ? n = ptr[] >> 3 : len += 1;
  );

  ptr[] = ((n + 1) << 3) | id;

  atomic_set(mutex, 0);
);

log_section(0, 1);

@slider
log_section(1, 1);

@block
log_section(2, 0);

@sample
log_section(3, 0);

@serialize
log_section(file_avail(0) < 0 ? 5 : 4, 1);
file_var(0, buf);

@gfx 312 312
log_section(6, 0);

function gfx_drawlog()
  global(buf, len, gfx_x, gfx_y, gfx_h)
  local(i, h, id, n)
(
  gfx_set(0, 1, 2/3);
  gfx_y = 8;

  h = ceil(max(gfx_h - 8, 0) / 12);

  i = max(len - h, 0);
  loop(min(len, h),
    gfx_x = 8;

    gfx_drawnumber(i, 0);
    gfx_x += 8;

    id = buf[i];
    i += 1;

    n = id >> 3;
    id &= 7;

    id == 0 ? gfx_drawstr("@init") :
    id == 1 ? gfx_drawstr("@slider") :
    id == 2 ? gfx_drawstr("@block") :
    id == 3 ? gfx_drawstr("@sample") :
    id == 4 ? gfx_drawstr("@serialize [r]") :
    id == 5 ? gfx_drawstr("@serialize [w]") :
    id == 6 ? gfx_drawstr("@gfx") :
    gfx_drawstr("@wtf");

    n > 1 ? (
      gfx_x += 8;
      gfx_drawnumber(n, 0);
    );

    gfx_y += 12;
  );
);

gfx_drawlog();
